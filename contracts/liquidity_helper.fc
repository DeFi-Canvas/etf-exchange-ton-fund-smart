#include "imports/stdlib.fc";
#include "imports/op-codes.fc";
#include "imports/messages.fc";


global slice storage::owner_addr;
global slice storage::fund_addr;
global cell storage::jettons_data;
global int storage::counter;

() load_data() impure {
  slice ds = get_data().begin_parse();
  storage::owner_addr = ds~load_msg_addr();
  storage::fund_addr = ds~load_msg_addr();
  storage::jettons_data = ds~load_dict();
  storage::counter = ds~load_uint(8);
}

() save_data() impure inline {
  set_data(
    begin_cell()
        .store_slice(storage::owner_addr)
        .store_slice(storage::fund_addr)
        .store_dict(storage::jettons_data)
        .store_uint(storage::counter, 8)
    .end_cell()
  );
}


() recv_internal(int my_balance, int msg_value, cell in_msg_full, slice in_msg_body) impure {
  if (in_msg_body.slice_empty?()) { ;; ignore empty messages
    return ();
  }

  slice cs = in_msg_full.begin_parse();
  int flags = cs~load_uint(4);
  if (flags & 1) {
    return ();
  }
  slice sender_address = cs~load_msg_addr();
  
  load_data();

  int op = in_msg_body~load_uint(32);
  int query_id = in_msg_body~load_uint(64);
  if (op == op::excesses()) {
    return ();
  }
  throw_unless(0xffff, op == op::deposit_liquidity());
  throw_unless(111, equal_slices(storage::fund_addr, sender_address));
  slice jetton_wallet = in_msg_body~load_msg_addr();
  int jetton_amount = in_msg_body~load_coins();
  (int wc, int addr) = parse_std_addr(jetton_wallet);
  slice value = begin_cell().store_coins(jetton_amount).end_cell().begin_parse();
  storage::jettons_data~udict_add?(256, addr, value);
  storage::counter += 1;
  if (storage::counter == 3) {
    var msg = begin_cell()
        .store_uint(op::deposit_liquidity(), 32)
        .store_uint(query_id, 64)
        .store_slice(storage::owner_addr)
        .store_dict(storage::jettons_data)
        .end_cell();
    send_simple_message(0, storage::fund_addr, msg, 128);
    storage::jettons_data = new_dict();
    storage::counter = 0;
  }
  save_data();
  return ();
}

;; (int, slice, slice, cell) get_wallet_data() method_id {
;;   return load_data();
;; }
